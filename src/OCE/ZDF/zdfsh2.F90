MODULE zdfsh2
   !!======================================================================
   !!                       ***  MODULE  zdfsh2  ***
   !! Ocean physics:  shear production term of TKE
   !!=====================================================================
   !! History :   -   !  2014-10  (A. Barthelemy, G. Madec)  original code
   !!   NEMO     4.0  !  2017-04  (G. Madec)  remove u-,v-pts avm
   !!   NEMO     4.2  !  2020-12  (G. Madec, E. Clementi) add Stokes Drift Shear
   !                  !           for wave coupling
   !!   NEMO     5.0  !  2024-06  (J. Chanut, G. Madec, S. Techene) adapt for RK3
   !!----------------------------------------------------------------------

   !!----------------------------------------------------------------------
   !!   zdf_sh2       : compute mixing the shear production term of TKE
   !!----------------------------------------------------------------------
   USE oce
   USE dom_oce        ! domain: ocean
   USE sbcwave        ! Surface Waves (add Stokes shear)
   USE sbc_oce , ONLY : ln_stshear  !Stoked Drift shear contribution
   !
   USE in_out_manager ! I/O manager
   USE lib_mpp        ! MPP library

   IMPLICIT NONE
   PRIVATE

   PUBLIC   zdf_sh2        ! called by zdftke, zdfglf, and zdfric

   !! * Substitutions
#  include "do_loop_substitute.h90"
#  include "domzgr_substitute.h90"
   !!----------------------------------------------------------------------
   !! NEMO/OCE 5.0, NEMO Consortium (2024)
   !! Software governed by the CeCILL license (see ./LICENSE)
   !!----------------------------------------------------------------------
CONTAINS

   SUBROUTINE zdf_sh2( Kbb, Kaa, p_avm, p_sh2  )
      !!----------------------------------------------------------------------
      !!                   ***  ROUTINE zdf_sh2  ***
      !!
      !! ** Purpose :   Compute the shear production term of a TKE equation
      !!
      !! ** Method  : - a stable discretization of this term is linked to the
      !!                time-space discretization of the vertical diffusion
      !!                of the OGCM. NEMO uses C-grid, a leap-frog environment
      !!                and an implicit computation of vertical mixing term,
      !!                so the shear production at w-point is given by:
      !!                   sh2 = mi[   mi(avm) * dk[ub]/e3ub * dk[un]/e3un   ]
      !!                       + mj[   mj(avm) * dk[vb]/e3vb * dk[vn]/e3vn   ]
      !!                NB: wet-point only horizontal averaging of shear
      !!
      !! ** Action  : - p_sh2 shear prod. term at w-point (inner domain only)
      !!                                                   *****
      !! References :   Bruchard, OM 2002
      !! ---------------------------------------------------------------------
      INTEGER                          , INTENT(in   ) ::   Kbb, Kaa             ! ocean time level indices
      REAL(wp), DIMENSION(jpi,jpj,jpk) , INTENT(in   ) ::   p_avm                ! vertical eddy viscosity (w-points)
      REAL(wp), DIMENSION(A2D(0) ,jpk) , INTENT(  out) ::   p_sh2                ! shear production of TKE (w-points)
      !
      INTEGER  ::   ji, jj, jk   ! dummy loop arguments
      REAL(wp), DIMENSION(A2D(1)) ::   zsh2u, zsh2v   ! 2D workspace
      !!--------------------------------------------------------------------
      !
      DO jk = 2, jpkm1                 !* Shear production at uw- and vw-points (energy conserving form)
         IF ( cpl_sdrftx .AND. ln_stshear )  THEN       ! Surface Stokes Drift available  ===>>>  shear + stokes drift contibution
            DO_2D( 1, 0, 1, 0 )
               zsh2u(ji,jj) =  ( p_avm(ji+1,jj,jk) + p_avm(ji,jj,jk) )            &
                  & * (  0.5_wp * ( uu (ji,jj,jk-1,Kbb) -   uu (ji,jj,jk,Kbb)     &
                  &               + uu (ji,jj,jk-1,Kaa) -   uu (ji,jj,jk,Kaa) )   &
                  &               + usd(ji,jj,jk-1    ) -   usd(ji,jj,jk    )  )  &
                  &             * ( uu (ji,jj,jk-1,Kaa) -   uu (ji,jj,jk,Kaa) )   &
                  &             / ( e3uw(ji,jj,jk,Kbb) * e3uw(ji,jj,jk,Kaa) ) * wumask(ji,jj,jk)
               zsh2v(ji,jj) =  ( p_avm(ji,jj+1,jk) + p_avm(ji,jj,jk) )            &
                  & * (  0.5_wp * ( vv (ji,jj,jk-1,Kbb) -   vv (ji,jj,jk,Kbb)     &
                  &               + vv (ji,jj,jk-1,Kaa) -   vv (ji,jj,jk,Kaa) )   &
                  &               + vsd(ji,jj,jk-1    ) -   vsd(ji,jj,jk    )  )  &
                  &             * ( vv (ji,jj,jk-1,Kaa) -   vv (ji,jj,jk,Kaa) )   &
                  &             / ( e3vw(ji,jj,jk,Kbb) * e3vw(ji,jj,jk,Kaa) ) * wvmask(ji,jj,jk)
            END_2D
         ELSE
            DO_2D( 1, 0, 1, 0 )
               zsh2u(ji,jj) =  ( p_avm(ji+1,jj,jk) + p_avm(ji,jj,jk) )        &
                  & * 0.5_wp * ( uu (ji,jj,jk-1,Kbb) -   uu (ji,jj,jk,Kbb)    &
                  &            + uu (ji,jj,jk-1,Kaa) -   uu (ji,jj,jk,Kaa) )  &
                  &          * ( uu (ji,jj,jk-1,Kaa) -   uu (ji,jj,jk,Kaa) )  &
                  &          / ( e3uw(ji,jj,jk,Kbb) * e3uw(ji,jj,jk,Kaa) ) * wumask(ji,jj,jk)
               zsh2v(ji,jj) = ( p_avm(ji,jj+1,jk) + p_avm(ji,jj,jk) )         &
                  & * 0.5_wp * ( vv (ji,jj,jk-1,Kbb) -   vv (ji,jj,jk,Kbb)    &
                  &            + vv (ji,jj,jk-1,Kaa) -   vv (ji,jj,jk,Kaa) )  &
                  &          * ( vv (ji,jj,jk-1,Kaa) -   vv (ji,jj,jk,Kaa) )  &
                  &          / ( e3vw(ji,jj,jk,Kbb) * e3vw(ji,jj,jk,Kaa) ) * wvmask(ji,jj,jk)
            END_2D
         ENDIF
         DO_2D( 0, 0, 0, 0 )     !* shear production at w-point ! coast mask: =2 at the coast ; =1 otherwise (NB: wmask useless as zsh2 are masked)
            p_sh2(ji,jj,jk) = 0.25_wp * (   ( zsh2u(ji-1,jj) + zsh2u(ji,jj) ) * ( 2._wp - umask(ji-1,jj,jk) * umask(ji,jj,jk) )   &
               &                          + ( zsh2v(ji,jj-1) + zsh2v(ji,jj) ) * ( 2._wp - vmask(ji,jj-1,jk) * vmask(ji,jj,jk) )   )
         END_2D
      END DO
      DO_2D( 0, 0, 0, 0 ) ! set p_sh2 to 0 at the surface and bottom for output purpose
         p_sh2(ji,jj,1)   = 0._wp
         p_sh2(ji,jj,jpk) = 0._wp
      END_2D
      !
   END SUBROUTINE zdf_sh2

   !!======================================================================
END MODULE zdfsh2
